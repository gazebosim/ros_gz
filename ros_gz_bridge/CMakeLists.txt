cmake_minimum_required(VERSION 3.5)

project(ros_gz_bridge)

# Default to C++14
if(NOT CMAKE_CXX_STANDARD)
  set(CMAKE_CXX_STANDARD 14)
endif()
if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wpedantic")
endif()

find_package(ament_cmake REQUIRED)
find_package(builtin_interfaces REQUIRED)
find_package(geometry_msgs REQUIRED)
find_package(nav_msgs REQUIRED)
find_package(rclcpp REQUIRED)
find_package(ros_gz_interfaces REQUIRED)
find_package(rosgraph_msgs REQUIRED)
find_package(sensor_msgs REQUIRED)
find_package(std_msgs REQUIRED)
find_package(tf2_msgs REQUIRED)
find_package(trajectory_msgs REQUIRED)

# TODO(CH3): Deprecated. Remove on tock.
if("$ENV{GZ_VERSION}" STREQUAL "" AND NOT "$ENV{IGNITION_VERSION}" STREQUAL "")
  message(DEPRECATION "Environment variable [IGNITION_VERSION] is deprecated. Use [GZ_VERSION] instead.")
  set(ENV{GZ_VERSION} $ENV{IGNITION_VERSION})
endif()

# Edifice
if("$ENV{GZ_VERSION}" STREQUAL "edifice")
  find_package(ignition-transport10 REQUIRED)
  set(GZ_TRANSPORT_VER ${ignition-transport10_VERSION_MAJOR})

  find_package(ignition-msgs7 REQUIRED)
  set(GZ_MSGS_VER ${ignition-msgs7_VERSION_MAJOR})

  set(GZ_TARGET_PREFIX ignition)

  message(STATUS "Compiling against Ignition Edifice")
# Garden
elseif("$ENV{GZ_VERSION}" STREQUAL "garden")
  find_package(gz-transport12 REQUIRED)
  set(GZ_TRANSPORT_VER ${gz-transport12_VERSION_MAJOR})

  find_package(gz-msgs9 REQUIRED)
  set(GZ_MSGS_VER ${gz-msgs9_VERSION_MAJOR})

  set(GZ_TARGET_PREFIX gz)

  message(STATUS "Compiling against Gazebo Garden")
# Default to Fortress
else()
  find_package(ignition-transport11 REQUIRED)
  set(GZ_TRANSPORT_VER ${ignition-transport11_VERSION_MAJOR})

  find_package(ignition-msgs8 REQUIRED)
  set(GZ_MSGS_VER ${ignition-msgs8_VERSION_MAJOR})

  set(GZ_TARGET_PREFIX ignition)

  message(STATUS "Compiling against Ignition Fortress")
endif()

set(PACKAGES
  builtin_interfaces
  geometry_msgs
  nav_msgs
  ros_gz_interfaces
  rosgraph_msgs
  sensor_msgs
  std_msgs
  tf2_msgs
  trajectory_msgs
)

add_library(bridge_utils
  STATIC
  src/convert/utils.cpp
)
target_include_directories(bridge_utils
  PRIVATE src
)
set_target_properties(bridge_utils PROPERTIES POSITION_INDEPENDENT_CODE ON)

foreach(PKG ${PACKAGES})
  add_library(${PKG}_bridge
    STATIC
    src/convert/${PKG}.cpp
    src/factories/${PKG}.cpp
  )
  target_include_directories(${PKG}_bridge
    PUBLIC include
    PRIVATE src
  )
  ament_target_dependencies(${PKG}_bridge
    "${PKG}"
  )
  target_link_libraries(${PKG}_bridge
    bridge_utils
    ${GZ_TARGET_PREFIX}-msgs${GZ_MSGS_VER}::core
    ${GZ_TARGET_PREFIX}-transport${GZ_TRANSPORT_VER}::core
    rclcpp::rclcpp
  )
  set_target_properties(${PKG}_bridge
    PROPERTIES POSITION_INDEPENDENT_CODE ON)

endforeach()

set(bridge_executables
  parameter_bridge
  static_bridge
)

set(bridge_lib
  ros_gz_bridge_lib
)

add_library(${bridge_lib}
  SHARED
  src/factories.cpp
  src/factory_interface.cpp
  src/service_factories/ros_gz_interfaces.cpp
)

target_include_directories(${bridge_lib}
  PUBLIC include
  PRIVATE src
)

foreach(PKG ${PACKAGES})
  target_link_libraries(${bridge_lib} ${PKG}_bridge)
endforeach()

install(TARGETS ${bridge_lib}
        ARCHIVE DESTINATION lib
        LIBRARY DESTINATION lib
        RUNTIME DESTINATION bin)

install(
  DIRECTORY include/
  DESTINATION include
)

ament_export_include_directories(include)
ament_export_libraries(${bridge_lib})

foreach(bridge ${bridge_executables})
  add_executable(${bridge}
    src/${bridge}.cpp
  )
  target_link_libraries(${bridge}
    ${bridge_lib}
    ${GZ_TARGET_PREFIX}-msgs${GZ_MSGS_VER}::core
    ${GZ_TARGET_PREFIX}-transport${GZ_TRANSPORT_VER}::core
  )
  ament_target_dependencies(${bridge}
    "rclcpp"
    ${PACKAGES}
  )
  install(TARGETS ${bridge}
    DESTINATION lib/${PROJECT_NAME}
  )
endforeach()

if(BUILD_TESTING)
  find_package(ament_lint_auto REQUIRED)
  ament_lint_auto_find_test_dependencies()

  find_package(ament_cmake_gtest REQUIRED)
  find_package(launch_testing_ament_cmake REQUIRED)
  ament_find_gtest()

  add_library(test_utils
    test/utils/gz_test_msg.cpp
    test/utils/ros_test_msg.cpp
  )

  target_include_directories(test_utils
    PUBLIC test ${GTEST_INCLUDE_DIRS}
  )
  target_link_libraries(test_utils
    ${GTEST_LIBRARIES}
    ${GZ_TARGET_PREFIX}-msgs${GZ_MSGS_VER}::core
    ${GZ_TARGET_PREFIX}-transport${GZ_TRANSPORT_VER}::core
  )
  ament_target_dependencies(test_utils
    builtin_interfaces
    geometry_msgs
    nav_msgs
    rclcpp
    ros_gz_interfaces
    rosgraph_msgs
    sensor_msgs
    std_msgs
    tf2_msgs
    trajectory_msgs
  )

  add_executable(test_ros_publisher test/publishers/ros_publisher.cpp)
  target_link_libraries(test_ros_publisher test_utils)

  add_executable(test_ros_subscriber
        test/subscribers/ros_subscriber/ros_subscriber.cpp
        test/subscribers/ros_subscriber/builtin_interfaces_subscriber.cpp
        test/subscribers/ros_subscriber/geometry_msgs_subscriber.cpp
        test/subscribers/ros_subscriber/nav_msgs_subscriber.cpp
        test/subscribers/ros_subscriber/ros_gz_interfaces_subscriber.cpp
        test/subscribers/ros_subscriber/rosgraph_msgs_subscriber.cpp
        test/subscribers/ros_subscriber/sensor_msgs_subscriber.cpp
        test/subscribers/ros_subscriber/std_msgs_subscriber.cpp
        test/subscribers/ros_subscriber/trajectory_msgs_subscriber.cpp)

  target_link_libraries(test_ros_subscriber test_utils)

  add_executable(test_gz_publisher test/publishers/gz_publisher.cpp)
  target_link_libraries(test_gz_publisher test_utils)

  add_executable(test_gz_subscriber test/subscribers/gz_subscriber.cpp)
  target_link_libraries(test_gz_subscriber test_utils)

  add_executable(test_gz_server test/serverclient/gz_server.cpp)
  target_link_libraries(test_gz_server test_utils)

  add_executable(test_ros_client test/serverclient/ros_client.cpp)
  target_link_libraries(test_ros_client test_utils)

  install(TARGETS
    test_ros_client
    test_ros_publisher
    test_ros_subscriber
    test_gz_publisher
    test_gz_server
    test_gz_subscriber
    DESTINATION lib/${PROJECT_NAME}
  )

  add_launch_test(test/launch/test_ros_subscriber.launch.py
    TIMEOUT 200
  )

  add_launch_test(test/launch/test_gz_subscriber.launch.py
    TIMEOUT 200
  )

  add_launch_test(test/launch/test_gz_server.launch.py
    TIMEOUT 200
  )
endif()

ament_export_dependencies(
  rclcpp
  ${PACKAGES}
)

ament_package()
